import Toybox.Application;
import Toybox.WatchUi;

import GeneratedCode;

class SampleApp extends Application.AppBase {
  function getInitialView() {
    return [ new SampleView() ];
  }
}

class SampleView extends WatchUi.View {
  var background;
  var widthScaledLabel;
  var heightScaledLabel;
  var fontHeightScaledSymbol_tiny;
  var fontHeightScaledSymbol_large;

  function initialize() {
    WatchUi.View.initialize();
  }

  function onLayout(dc) {
    // The Rez.Drawables resources are generated by the build targets.
    background = loadResource(Rez.Drawables.Background);
    widthScaledLabel = loadResource(Rez.Drawables.WidthScaledLabel);
    heightScaledLabel = loadResource(Rez.Drawables.HeightScaledLabel);

    // Load these parametrically-built resources if present. They might be
    // absent when the font files associated with font sizes are TTF (vector)
    // rather than CFT.
    if (Rez.Drawables has :FontHeightScaledSymbol_tiny) {
      fontHeightScaledSymbol_tiny = loadResource(Rez.Drawables.FontHeightScaledSymbol_tiny);
    }
    if (Rez.Drawables has :FontHeightScaledSymbol_large) {
      fontHeightScaledSymbol_large = loadResource(Rez.Drawables.FontHeightScaledSymbol_large);
    }
  }

  function onUpdate(dc) {
    System.println(Time.now().value() + ": updating...");

    dc.drawBitmap(0, 0, background);

    var xc = dc.getWidth() / 2;
    var yc = dc.getHeight() / 2;

    dc.drawBitmap(
      xc - widthScaledLabel.getWidth() / 2,
      0,
      widthScaledLabel);
    dc.drawBitmap(
      0,
      yc - heightScaledLabel.getHeight() / 2,
      heightScaledLabel);

    dc.setColor(Graphics.COLOR_BLACK, Graphics.COLOR_TRANSPARENT);

    // Draw text and icon to match tiny font
    if (Rez.Drawables has :FontHeightScaledSymbol_tiny) {
      dc.drawText(
          xc + fontHeightScaledSymbol_tiny.getWidth() / 2,
          yc - fontHeightScaledSymbol_tiny.getHeight(),
          Graphics.FONT_TINY,
          "TINY",
          Graphics.TEXT_JUSTIFY_LEFT
      );
      dc.drawBitmap(
        xc - fontHeightScaledSymbol_tiny.getWidth() / 2,
        yc - fontHeightScaledSymbol_tiny.getHeight(),
        fontHeightScaledSymbol_tiny
      );
    }

    // Draw text and icon to match large font
    if (Rez.Drawables has :FontHeightScaledSymbol_tiny) {
      dc.drawText(
          xc + fontHeightScaledSymbol_large.getWidth() / 2,
          yc,
          Graphics.FONT_LARGE,
          "LARGE",
          Graphics.TEXT_JUSTIFY_LEFT
      );
      dc.drawBitmap(
        xc - fontHeightScaledSymbol_large.getWidth() / 2,
        yc,
        fontHeightScaledSymbol_large
      );
    }

    dc.setColor(Graphics.COLOR_WHITE, Graphics.COLOR_TRANSPARENT);

    // Use the generated code to display the device's display type.
    dc.drawText(
      xc, dc.getHeight() - dc.getFontHeight(Graphics.FONT_SMALL) - 10,
      Graphics.FONT_SMALL,
      GeneratedCode.DISPLAY_TYPE,
      Graphics.TEXT_JUSTIFY_CENTER
    );
  }
}