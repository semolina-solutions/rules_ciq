"""Demonstrates the use of build tools to generate device-specific resources.

Portrait rectangular screen demo:
`bazel run @rules_ciq//samples/device_parametrics:sample_edge820_debug_simulation`

Landscape rectangular screen demo:
`bazel run @rules_ciq//samples/device_parametrics:sample_epix_debug_simulation`
"""

load(
    "@rules_ciq//:defs.bzl",
    "ciq_jungle",
    "ciq_manifest",
    "ciq_project",
    "ciq_scaled_drawable_jungle",
    "devices",
    "supports_app_type",
)
load("@rules_ciq//:macros.bzl", "device_targets_macro")
load(":helpers.bzl", "build_font_height_scaled_symbol_jungles", "device_parameterized_source_jungle")

LAUNCHER_ICON_RESOURCE_ID = "LauncherIcon"

# Generates device-specific scaled icons.
ciq_scaled_drawable_jungle(
    name = "scaled_icon",
    src = ":icon.png",
    mode = "icon",
    resource_id = LAUNCHER_ICON_RESOURCE_ID,
)

# Generate device-specific scaled background images, scaled to fill the screen
# (the resulting image may overflow in width or height).
ciq_scaled_drawable_jungle(
    name = "screen_filled_background",
    src = ":background.jpg",
    mode = "screen_fill",
    resource_id = "Background",
)

# Generates device-specific scaled label images, scaled to be 50% of the
# screen's width.
ciq_scaled_drawable_jungle(
    name = "screen_width_scaled_label",
    src = ":scaled_to_width.png",
    mode = "screen_width",
    percent = 50,
    resource_id = "WidthScaledLabel",
)

# Generate device-specific scaled label images, scaled to be 50% of the
# screen's height.
ciq_scaled_drawable_jungle(
    name = "screen_height_scaled_label",
    src = ":scaled_to_height.png",
    mode = "screen_height",
    percent = 50,
    resource_id = "HeightScaledLabel",
)

# Runs a custom generator to generate device-specific sources.
device_parameterized_source_jungle(
    name = "device_parameterized_sources",
)

# Static project files.
ciq_jungle(
    name = "sample_jungle",
    resources = glob(["*.xml"]),
    sources = glob(["*.mc"]),
)

ciq_manifest(
    name = "sample_manifest",
    entry = "SampleApp",
    id = "00000000-0000-0000-0000-000000000000",
    launcher_icon_drawable_resource_id = LAUNCHER_ICON_RESOURCE_ID,
    min_api_level = "1.0.0",
    name_string_resource_id = "SampleAppName",
    type = "widget",
)

# All jungles used in the project, included in the order they are listed.
JUNGLES = [
    ":sample_jungle",
    ":scaled_icon",
    ":screen_filled_background",
    ":screen_width_scaled_label",
    ":screen_height_scaled_label",
    ":device_parameterized_sources",
] + build_font_height_scaled_symbol_jungles()

ciq_project(
    name = "sample_project",
    jungles = JUNGLES,
    manifest = ":sample_manifest",
)

DEVICE_IDS_SUPPORTING_WIDGET = [
    device_id
    for device_id in devices
    if supports_app_type(device_id, "widget")
]

# Generates device-specific targets for the sample project. See macros.bzl for
# more information.
device_targets_macro(
    name = "sample",
    device_ids = DEVICE_IDS_SUPPORTING_WIDGET,
    project = ":sample_project",
)
